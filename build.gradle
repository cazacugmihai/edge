import com.darylteo.gradle.vertx.VertxPlugin

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }  // for gradle plugin
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // for vertx core
  }

  dependencies { classpath "com.darylteo:vertx-gradle-plugin:1.0.0-ALPHA4-SNAPSHOT" }
}

subprojects {
    apply plugin: VertxPlugin
}

project(':edge-core') {
  dependencies {
    modules vertxModule('com.darylteo~vertx-promises~1.0.0-ALPHA1-SNAPSHOT')
    modules vertxModule('com.jetdrone~yoke~1.0.0-SNAPSHOT')
  }
}

project(':edge-daemon') {
  apply plugin: 'application'

  mainClassName = 'org.edgeframework.daemon.Daemon'
  applicationName = 'edge'
  
  repositories {
    maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }  // for gradle plugin
  }

  dependencies {
    compile "org.gradle:gradle-tooling-api:${toolingApiVersion}"
    compile "com.darylteo:directorywatcher:${directoryWatcherVersion}"
  }
}

project(':edge-builder') {
  apply plugin: 'groovy'

  dependencies {
    compile gradleApi()
    compile localGroovy()
  }
}