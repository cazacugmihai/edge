import com.darylteo.gradle.plugins.vertx.*
import com.darylteo.gradle.plugins.*

buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies { classpath "com.darylteo:vertx-gradle-plugin:1.1.0-ALPHA3-SNAPSHOT" }
}

project(':edge-core') {
  apply plugin: VertxProjectPlugin
  apply plugin: VertxPublishPlugin

  vertx {
    version = '2.0.1-final'

    config { include 'com.jetdrone~yoke~1.0.1' }
  }
}

project(':edge-daemon') {
  apply plugin: 'application'

  mainClassName = 'org.edgeframework.daemon.Daemon'
  applicationName = 'edge'

  repositories {
    maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }  // for gradle plugin
    mavenLocal()
  }

  dependencies {
    compile "io.vertx:vertx-platform:${vertxVersion}"
    compile "org.gradle:gradle-tooling-api:${toolingApiVersion}"
    compile "com.darylteo:directorywatcher:${directoryWatcherVersion}"
  }

  task installLocal(type:Sync, dependsOn: installApp) {
    from installApp
    into '/usr/local/bin/edge/'
  }
}

project(':edge-builder') {
  apply plugin: 'groovy'

  dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "com.darylteo:vertx-gradle-plugin:1.1.0-ALPHA1-SNAPSHOT"
  }
}

subprojects {
  apply plugin: MavenPlugin

  repositories {
    mavenLocal();
    mavenCentral();
  }
}