import com.darylteo.gradle.plugins.vertx.*
import com.darylteo.gradle.plugins.*

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    classpath "com.darylteo:vertx-gradle-plugin:1.1.0-ALPHA1-SNAPSHOT"
  }
}

subprojects {
  apply plugin: MavenPlugin

  repositories {
    mavenLocal();
    mavenCentral();
  }
}

project(':edge-core') {
  dependencies {
    runtime "io.vertx:vertx-platform:2.0.0-CR2"
  }
  
  apply plugin: VertxProjectPlugin
  apply plugin: VertxPublishPlugin

  vertx {
    config {
      includes = [
        'com.darylteo~vertx-promises~1.1.0-SNAPSHOT',
        'com.jetdrone~yoke~1.0.0-beta2'
      ]
    }
  }
}

project(':edge-daemon') {
  apply plugin: 'application'

  mainClassName = 'org.edgeframework.daemon.Daemon'
  applicationName = 'edge'

  repositories {
    maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }  // for gradle plugin
  }

  dependencies {
    compile "org.gradle:gradle-tooling-api:${toolingApiVersion}"
    compile "com.darylteo:directorywatcher:${directoryWatcherVersion}"
  }
}

project(':edge-builder') {
  apply plugin: 'groovy'

  dependencies {
    compile gradleApi()
    compile localGroovy()
  }
}