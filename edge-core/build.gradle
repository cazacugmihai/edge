buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    classpath "com.darylteo.vertx:vertx-gradle-plugin:0.1.0-SNAPSHOT"
  }
}

def testdir = file('testdir')
def apiProject = childProjects.'edge-core-api'
def langProjects = subprojects - apiProject

subprojects {
  repositories {
    mavenCentral()
    mavenLocal()
  }

  apply plugin: 'vertx'
  apply plugin: 'maven'

  version = '0.1.0-SNAPSHOT'

  vertx.platform.version '2.0.2-final'

  test.workingDir = testdir

  ext.release = !project.version.endsWith('-SNAPSHOT')

  task sourcesJar(type:Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
  }

  task javadocJar(type:Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }
}

configure(apiProject) {
  vertx {
    platform {
      java '2.0.2-final'
    }
  }
}

configure(langProjects) {
  apply from: parent.file('gradle/generation.gradle')
  
  artifacts { archives modZip }

  uploadArchives {
    repositories {
      mavenDeployer { configuration = configurations.archives }
    }
  }
}

task('install') { dependsOn subprojects*.install }